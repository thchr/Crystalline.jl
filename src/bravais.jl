"""
    crystal(a,b,c,Î±,Î²,Î³) --> Rs::DirectBasis{3}

Calculate basis vectors ``\\mathbf{R}_1``, ``\\mathbf{R}_2``, ``\\mathbf{R}_3`` in a 3D
Cartesian basis for a right-handed coordinate system with specified basis vector lengths
`a`, `b`, `c` (associated with ``\\mathbf{R}_1``, ``\\mathbf{R}_2``, & ``\\mathbf{R}_3``,
respectively) and specified interaxial angles 
`Î±` ``= âˆ (\\mathbf{R}_2,\\mathbf{R}_3)``, `Î²` ``= âˆ (\\mathbf{R}_3,\\mathbf{R}_1)``, 
`Î³` ``= âˆ (\\mathbf{R}_1,\\mathbf{R}_2)``, with ``âˆ `` denoting the angle between two vectors.

For definiteness, the ``\\mathbf{R}_1`` basis vector is oriented along the ``x``-axis of the
Cartesian coordinate system, and the ``\\mathbf{R}_2`` axis is placed in the ``xy``-plane.
"""
function crystal(a::Real, b::Real, c::Real, Î±::Real, Î²::Real, Î³::Real)
    # consistency checks on interaxial angles (equivalently, sides of the corresponding unit-spherical triangle)
    if !isvalid_sphericaltriangle(Î±,Î²,Î³)
        throw(DomainError((Î±,Î²,Î³), "The provided angles Î±,Î²,Î³ cannot be mapped to a spherical triangle, and thus do not form a valid axis system"))
    end
    sinÎ³, cosÎ³ = sincos(Î³)
    # Râ‚ and Râ‚‚ are easy
    Râ‚ = SVector{3,Float64}(a, 0.0, 0.0)
    Râ‚‚ = SVector{3,Float64}(b*cosÎ³, b*sinÎ³, 0.0)
    # R3 is harder
    cosÎ± = cos(Î±)
    cosÎ² = cos(Î²)
    Ï• = atan(cosÎ± - cosÎ³*cosÎ², sinÎ³*cosÎ²)
    Î¸ = asin(sign(Î²)*sqrt(cosÎ±^2 + cosÎ²^2 - 2*cosÎ±*cosÎ³*cosÎ²)/abs(sin(Î³))) # more stable than asin(cosÎ²/cosÏ•) when Î² or Î³ â‰ˆ Ï€/2
    sinÎ¸, cosÎ¸ = sincos(Î¸)
    sinÏ•, cosÏ• = sincos(Ï•)
    Râ‚ƒ = SVector{3,Float64}(c.*(sinÎ¸*cosÏ•, sinÎ¸*sinÏ•, cosÎ¸))

    Rs = DirectBasis(Râ‚, Râ‚‚, Râ‚ƒ)
    return Rs
end

"""
    crystal(a,b,Î³) --> DirectBasis{2}

Calculate basis vectors ``\\mathbf{R}_1``, ``\\mathbf{R}_2`` in a 2D Cartesian basis for a 
right-handed coordinate system with specified basis vector lengths `a`, `b` (associated with
``\\mathbf{R}_1`` & ``\\mathbf{R}_2``, respectively) and specified interaxial angle
`Î³` ``= âˆ (\\mathbf{R}_1,\\mathbf{R}_2)``.

For definiteness, the ``\\mathbf{R}_1`` basis vector is oriented along the ``x``-axis of the
Cartesian coordinate system.
"""
function crystal(a::Real,b::Real,Î³::Real) 
    Râ‚ = SVector{2,Float64}(a, 0.0)
    Râ‚‚ = SVector{2,Float64}(b.*(cos(Î³), sin(Î³)))

    return DirectBasis(Râ‚,Râ‚‚)
end

"""
    crystal(a)  --> DirectBasis{1}
    
Return a one-dimensional crystal with lattice period `a`.
"""
crystal(a::Real) = DirectBasis(SVector{1,Float64}(float(a)))

# For a three-axis system, Î±, Î², and Î³ are subject to constraints: specifically, 
# since they correspond to sides of a (unit-radius) spherical triangle, they 
# are subject to identical constraints. These constraints are
#     0 < Î± + Î² + Î³ < 2Ï€,                           (1)
#     sin(s-Î±)*sin(s-Î²)*sin(s-Î³)/sin(s) > 0,        (2)
# with s = (Î± + Î² + Î³)/2. Constraint (2) can be identified from Eq. (38) of 
# http://mathworld.wolfram.com/SphericalTrigonometry.html; due to (1), it can 
# be simplified to sin(s-Î±)*sin(s-Î²)*sin(s-Î³) > 0. This impacts generation 
# of triclinic and monoclinic crystals.
function isvalid_sphericaltriangle(Î±::Real, Î²::Real, Î³::Real)
    s = (Î±+Î²+Î³)/2
    check1 = zero(s) < s < Ï€
    check2 = sin(s-Î±)*sin(s-Î²)*sin(s-Î³) > zero(s)
    return check1 && check2
end

Â°(Ï†::Real) = deg2rad(Ï†)

""" 
    crystalsystem(R::DirectBasis{D})

Determine the crystal system of a point lattice specified in a 
*conventional* `DirectBasis{D}` of dimension `D` using
Tables 2.1.2.1, 9.1.7.1, & 9.1.7.2 of the International Tables of 
Crystallography, Volume 1 (ITA). 
There are 4 crystal systems in 2D and 7 in 3D (see ITA 2.1.2(iii)):

| `D`    | System       | Conditions             | Free parameters      |
|:-------|--------------|------------------------|----------------------|
| **1D** | linear       | none                   | a                    |
| **2D** | square       | a=b & Î³=90Â°            | a                    |
|        | rectangular  | Î³=90Â°                  | a,b                  |
|        | hexagonal    | a=b & Î³=120Â°           | a                    |
|        | oblique      | none                   | a,b,Î³                |
| **3D** | cubic        | a=b=c & Î±=Î²=Î³=90Â°      | a                    |
|        | hexagonal    | a=b & Î±=Î²=90Â° & Î³=120Â° | a,c                  |
|        | trigonal     | a=b & Î±=Î²=90Â° & Î³=120Â° | a,c (a,Î± for hR)     |
|        | tetragonal   | a=b & Î±=Î²=Î³=90Â°        | a,c                  |
|        | orthorhombic | Î±=Î²=Î³=90Â°              | a,b,c                |
|        | monoclinic   | Î±=Î³=90Â°                | a,b,c,Î²â‰¥90Â°          |
|        | triclinic    | none                   | a,b,c,Î±,Î²,Î³          |

The DirectBasis input is assumed to use *conventional* basis vectors; 
i.e. not necessarily primitive. For primitive basis vectors, the 
crystal system can be further reduced into 5 Bravais types in 2D and
14 in 3D.
"""
function crystalsystem(Rs::DirectBasis{D}) where D
    if D == 1
        # doesn't seem to exist a well-established convention for 1D? this is ours...
        system = "linear"
        
    elseif D == 2
        a,b = norms(Rs)
        Î³ = angles(Rs)
        if aâ‰ˆb && Î³â‰ˆÂ°(90)
            system = "square"
        elseif Î³â‰ˆÂ°(90)
            system = "rectangular"
        elseif aâ‰ˆb && Î³â‰ˆÂ°(120)
            system = "hexagonal"
        else
            system = "oblique"
        end

            
    elseif D == 3 
        # TODO: Generalize this to work for non-standard orientations of the lattice/lattice-vector orderings.
        #       If that is done, this can be safely applied to ReciprocalBasis as well (only a problem in 3D).
        a,b,c = norms(Rs)
        Î±,Î²,Î³ = angles(Rs)
        if aâ‰ˆbâ‰ˆc && Î±â‰ˆÎ²â‰ˆÎ³â‰ˆÂ°(90)             # cubic        (cP, cI, cF)
            system = "cubic"
        elseif aâ‰ˆb && Î³â‰ˆÂ°(120) && Î±â‰ˆÎ²â‰ˆÂ°(90) # hexagonal    (hR, hP)
            system = "hexagonal" 
        elseif aâ‰ˆbâ‰ˆc && Î±â‰ˆÎ²â‰ˆÎ³               # trigonal     (? hP, hI ?)
            system = "trigonal"
                # rhombohedral axes                   (a = b = c, Î±=Î²=Î³ < 120Â° â‰  90Â° ?)
                # hexagonal axes, triple obverse axes (a = b â‰  c, Î±=Î²=90Â°, Î³=120Â° ?)
        elseif aâ‰ˆb && Î±â‰ˆÎ²â‰ˆÎ³â‰ˆÂ°(90)           # tetragonal   (tP, tI) 
            system = "tetragonal"
        elseif Î±â‰ˆÎ²â‰ˆÎ³â‰ˆÂ°(90)                  # orthorhombic (oP, oI, oF, oC)
            system = "orthorhombic"
        elseif Î±â‰ˆÎ³â‰ˆÂ°(90)                    # monoclinic   (mP, mC)
            system = "monoclinic"
        else                                # triclinic    (aP)
            system = "triclinic"
        end
    else
        throw(DomainError("Support for higher-than-three dimensional lattices not implemented."))
    end
    return system
end


function crystalsystem(sgnum::Integer, D::Integer=3)
    if D == 1
        # doesn't seem to exist a well-established convention for 1D? this is ours...
        if      sgnum âˆˆ 1:2;   return "linear"       # lp
        else    throw(DomainError(sgnum, "There are only 2 one-dimensional line groups."))
        end
    elseif D == 2
        if      sgnum âˆˆ 1:2;   return "oblique"      # mp
        elseif  sgnum âˆˆ 3:9;   return "rectangular"  # op, oc
        elseif  sgnum âˆˆ 10:12; return "square"       # tp
        elseif  sgnum âˆˆ 13:17; return "hexagonal"    # hp
        else    throw(DomainError(sgnum, "There are only 17 two-dimensional plane groups."))
        end
    
    elseif D == 3
        if      sgnum âˆˆ 1:2;     return "triclinic"     # aP
        elseif  sgnum âˆˆ 3:15;    return "monoclinic"    # mP, mC
        elseif  sgnum âˆˆ 16:74;   return "orthorhombic"  # oP, oI, oF, oC
        elseif  sgnum âˆˆ 75:142;  return "tetragonal"    # tP, tI
        elseif  sgnum âˆˆ 143:167; return "trigonal"      # hR, hP
        elseif  sgnum âˆˆ 168:194; return "hexagonal"     # hR, hP
        elseif  sgnum âˆˆ 195:230; return "cubic"         # cP, cI, cF
        else    throw(DomainError(sgnum, "There are only 230 three-dimensional space groups."))
        end
    end
end

"""
    relrand(lims::NTuple{2,Real}) --> Float64

Computes a random number in the range specified by the two-element 
tuple `lims`. The random numbers are sampled from two uniform 
distributions, namely [`lims[1]`, 1] and [1, `lims[2]`], in such a
way as to ensure that the sampling is uniform over the joint  
interval [-1/`lims[1]`, -1] âˆª [1, `lims[2]`].

This is useful for ensuring an even sampling of numbers that are
either smaller or larger than unity. E.g. for `x = relrand((0.2,5.0))`,
`x` is equally probable to fall in inv(`x`)âˆˆ[1,5] or `x`âˆˆ[1,5].
"""
function relrand(lims::NTuple{2,<:Real})
    low, high = lims; invlow = inv(low)
    lowthres = (invlow - 1.0)/(invlow + high - 2.0)
    if rand() < lowthres && low < 1.0   # smaller than 1.0
        r = rand(_Uniform(low,1.0))
    elseif high > 1.0                   # bigger than 1.0
        r = rand(_Uniform(1.0,high))
    else                                # default
        return rand(_Uniform(low,high))
    end
end

""" 
    directbasis(sgnum, D=3;    abclims, Î±Î²Î³lims)
    directbasis(sgnum, Val(D); abclims, Î±Î²Î³lims) --> DirectBasis{D}

Return a (conventional) `DirectBasis` for a crystal compatible with 
the space group number `sgnum` and dimensionality `D`. Free parameters
in the lattice vectors are chosen randomly, with limits optionally
supplied in `abclims` (lengths) and `Î±Î²Î³lims` (angles).
By convention, the length of the first lattice vector (`a`) is set
to unity, such that the second and third (`b` and `c`) lattice 
vectors' lengths are relative to the first.

Limits on the relative uniform distribution of lengths `b` and `c`
can be specified as 2-tuple kwarg `abclims`; similarly, limits on 
the angles `Î±`, `Î²`, `Î³` can be set via Î±Î²Î³lims (only affects 
oblique, monoclinic, & triclinic lattices).
"""
function directbasis(sgnum::Integer, Dáµ›::Val{D}=Val(3);
                     abclims::NTuple{2,Real}=(0.5,2.0), 
                     Î±Î²Î³lims::NTuple{2,Real}=(Â°(30),Â°(150))) where D
    system = crystalsystem(sgnum, D)
    if D == 1
        a = 1.0
        return crystal(a)
    elseif D == 2
        if     system == "square"      # a=b & Î³=90Â° (free: a)
            a = b = 1.0
            Î³ = Â°(90)
        elseif system == "rectangular" # Î³=90Â° (free: a,b)
            a = 1.0;    b = relrand(abclims)
            Î³ = Â°(90)           
        elseif system == "hexagonal"   # a=b & Î³=120Â° (free: a)
            a = b = 1.0;
            Î³ = Â°(120)
        elseif system == "oblique"     # no conditions (free: a,b,Î³)
            a = 1.0;    b = relrand(abclims)
            Î³ = rand(_Uniform(Î±Î²Î³lims...)) 
        else 
            throw(DomainError(system))
        end
        return crystal(a,b,Î³)

    elseif D == 3
        if     system == "cubic"        # a=b=c & Î±=Î²=Î³=90Â° (free: a)
            a = b = c = 1.0
            Î± = Î² = Î³ = Â°(90)
        elseif system == "hexagonal" || # a=b & Î±=Î²=90Â° & Î³=120Â° (free: a,c)
               system == "trigonal"    
            a = b = 1.0;        c = relrand(abclims)
            Î± = Î² = Â°(90);      Î³ = Â°(120)
            # For the trigonal crystal system, a convention is adopted where 
            # the crystal basis matches the a hexagonal one, even when the 
            # Bravais type is rhombohedral (of course, the primitive basis 
            # differs). The conventional cell is always chosen to have (triple
            # obverse) hexagonal axes (see ITA7 Sec. 3.1.1.4, Tables 2.1.1.1, 
            # & 3.1.2.2).
            # For rhombohedral systems the primitive cell has a=b=c, Î±=Î²=Î³<120Â°â‰ 90Â°.
            # Note that the hexagonal and trigonal crystal systems also share 
            # the same crystal system abbreviation 'h' (see CRYSTALSYSTEM_ABBREV),
            # which already suggests this choice.
            # TODO: in principle, this means it would be more meaningful to 
            # branch on `CRYSTALSYSTEM_ABBREV[D][system]` than on `system`.
        elseif system == "tetragonal"   # a=b & Î±=Î²=Î³=90Â° (free: a,c)
            a = b = 1.0;        c = relrand(abclims)
            Î± = Î² = Î³ = Â°(90)
        elseif system == "orthorhombic" # Î±=Î²=Î³=90Â° (free: a,b,c)
            a = 1.0;            b, c = relrand(abclims), relrand(abclims)
            Î± = Î² = Î³ = Â°(90)
        elseif system == "monoclinic"   # Î±=Î³=90Â° (free: a,b,c,Î²â‰¥90Â°)
            a = 1.0;            b, c = relrand(abclims), relrand(abclims)
            Î± = Î³ = Â°(90);      Î² = rand(_Uniform(Â°(90), Î±Î²Î³lims[2]))
            while !isvalid_sphericaltriangle(Î±,Î²,Î³)  # arbitrary combinations of Î±,Î²,Î³ may not correspond 
                Î² = rand(_Uniform(Â°(90), Î±Î²Î³lims[2])) # to a valid axis-system; reroll until they do
            end
        elseif system == "triclinic"    # no conditions (free: a,b,c,Î±,Î²,Î³)
            a = 1.0;            b, c = relrand(abclims), relrand(abclims)
            U = _Uniform(Î±Î²Î³lims...)
            Î±, Î², Î³ = rand(U), rand(U), rand(U)
            while !isvalid_sphericaltriangle(Î±,Î²,Î³) # arbitrary combinations of Î±,Î²,Î³ may not correspond 
                Î±, Î², Î³ = rand(U), rand(U), rand(U) # to a valid axis-system; reroll until they do
            end
        else 
            throw(DomainError(system))
        end        
        return crystal(a,b,c,Î±,Î²,Î³)

    else 
        _throw_invaliddim(D)
    end
end
function directbasis(sgnum::Integer, D::Integer;
            abclims::NTuple{2,Real}=(0.5,2.0), Î±Î²Î³lims::NTuple{2,Real}=(Â°(30),Â°(150)))
    directbasis(sgnum, Val(D); abclims=abclims, Î±Î²Î³lims=Î±Î²Î³lims)
end


const CRYSTALSYSTEM_ABBREV = (ImmutableDict("linear"=>'l'),                                            # 1D
                              ImmutableDict("oblique"=>'m', "rectangular"=>'o', "square"=>'t',         # 2D
                                   "hexagonal"=>'h'),
                              ImmutableDict("triclinic"=>'a', "monoclinic"=>'m', "orthorhombic"=>'o',  # 3D
                                   "tetragonal"=>'t', "trigonal"=>'h', "hexagonal"=>'h', 
                                   "cubic"=>'c')
                             )

"""
    $(TYPEDSIGNATURES)

Return the Bravais type of `sgnum` in dimension `D` as a string (as the concatenation
of the single-character crystal abbreviation and the centering type).

## Keyword arguments

If the centering type associated with `sgnum` is `'A'`, we can choose (depending on the
keyword argument `normalize`, defaulting to `false`) to "normalize" to the centering type
`'C'`, since the difference between `A` and `C` centering only amounts to a basis change.
With `normalize=true` we then have only the canonical 14 Bravais type, i.e. 
`unique(bravaistype.(1:230, 3), normalize=true)` returns only 14 distinct types, rather
than 15.

This only affects space groups 38-41 (normalizing their conventional Bravais types from
`oA` to `oC`).
"""
@inline function bravaistype(sgnum::Integer, D::Integer=3; normalize::Bool=false)
    cntr = centering(sgnum, D)
    system = crystalsystem(sgnum, D)

    # If the centering type is 'A', then we could in fact always pick
    # the basis differently such that the centering would be 'C'; in 
    # other words, base-centered lattices at 'A' and 'C' in fact describe
    # the same Bravais lattice; there is no significance in trying to 
    # differentiate them - if we do, we end up with 15 Bravais lattices in 
    # 3D rather than 14: so we manually fix that here:
    if normalize
        cntr = cntr == 'A' ? 'C' : cntr
    end

    # pick the correct crystal system abbreviation from CRYSTALSYSTEM_ABBREV 
    # and return its concatenation with the (now-"normalized") centering type
    return CRYSTALSYSTEM_ABBREV[D][system]*cntr 
end



# Transformation matrices ğ from a (direct-space) conventional basis 
# (ğš ğ› ğœ) to a primitive basis (ğšâ‚š ğ›â‚š ğœâ‚š) via
#     (ğšâ‚š ğ›â‚š ğœâ‚š) = (ğš ğ› ğœ)ğ
# # with (ğš ğ› ğœ) and (ğšâ‚š ğ›â‚š ğœâ‚š) interpreted as column matrices the 
# transformation matrix ğ depends only on the centering type [note
# that centering type 'B' seems to not occur, by convention]
# The values of ğ are taken from Table 2 of the Aroyo's Bilbao 
# publication (https://doi.org/:10.1107/S205327331303091X), which 
# give the coefficients of (ğáµ€)â»Â¹. See also Hinuma's 2016 paper
# (https://doi.org/10.1016/j.commatsci.2016.10.015) for details,
# though note that they use different matrices for 'A' and complicate
# the 'C' scenario (Table 3).
const PRIMITIVE_BASIS_MATRICES = (
    # 1D
    ImmutableDict('p'=>SMatrix{1,1,Float64}(1)),                # primitive
    # 2D
    ImmutableDict('p'=>SMatrix{2,2,Float64}([1 0; 0 1]),        # primitive/simple
                  'c'=>SMatrix{2,2,Float64}([1 1; -1 1]./2)),   # centered      
    # 3D
    ImmutableDict(
        'P'=>SMatrix{3,3,Float64}([1 0 0; 0 1 0; 0 0 1]),       # primitive/simple
        'F'=>SMatrix{3,3,Float64}([0 1 1; 1 0 1; 1 1 0]./2),    # face-centered
        'I'=>SMatrix{3,3,Float64}([-1 1 1; 1 -1 1; 1 1 -1]./2), # body-centered
        'R'=>SMatrix{3,3,Float64}([2 -1 -1; 1 1 -2; 1 1 1]./3), # rhombohedrally-centered
        'A'=>SMatrix{3,3,Float64}([2 0 0; 0 1 -1; 0 1 1]./2),   # base-centered (along x)
        'C'=>SMatrix{3,3,Float64}([1 1 0; -1 1 0; 0 0 2]./2))   # base-centered (along z)
    )

"""
    primitivebasismatrix(cntr::Char, ::Val{D}=Val(3)) -> SMatrix{D,D,Float64}

Return the transformation matrix `P` that transforms a conventional unit cell with centering
`cntr` to the corresponding primitive unit cell (in dimension `D`).
"""
@inline function primitivebasismatrix(cntr::Char, ::Val{D}=Val(3)) where D
    Dâˆ‰1:3 && _throw_invaliddim(D)
    return PRIMITIVE_BASIS_MATRICES[D][cntr]
end

@inline function centeringtranslation(cntr::Char, ::Val{D}=Val(3)) where D
    if D == 3
        if cntr == 'P';     return zeros(SVector{3})
        elseif cntr == 'I'; return SVector((1,1,1)./2)
        elseif cntr == 'F'; return SVector((1,0,1)./2)
        elseif cntr == 'R'; return SVector((2,1,1)./3)
        elseif cntr == 'A'; return SVector((0,1,1)./2)
        elseif cntr == 'C'; return SVector((1,1,0)./2)
        else;               _throw_invalidcntr(cntr)
        end
    elseif D == 2
        if cntr == 'p';     return zeros(SVector{2})
        elseif cntr == 'c'; return SVector((1,1)./2)
        else;               _throw_invalidcntr(cntr)
        end
    elseif D == 1
        return zeros(SVector{1})
    else 
        _throw_invaliddim(D)
    end
end


"""
    reciprocalbasis(Rs::DirectBasis{D}) --> Gs::ReciprocalBasis{D}
    
Calculates the reciprocal basis associated with a `DirectBasis` `Rs`
(alternatively supplied as an `NTuple` of `Vector`s). Returns a `ReciprocalBasis`.
"""
function reciprocalbasis(Rs::Union{DirectBasis{D}, NTuple{D, Vector{<:Real}}}) where D
    if D == 3
        Gâ‚â€² = Rs[2]Ã—Rs[3]
        pref = 2Ï€/dot(Rs[1], Gâ‚â€²)
        vecs = pref .* (Gâ‚â€², Rs[3]Ã—Rs[1], Rs[1]Ã—Rs[2])
    elseif D == 2
        Gâ‚â€² = (@SVector [-Rs[2][2], Rs[2][1]])
        pref = 2Ï€/dot(Rs[1], Gâ‚â€²)
        vecs = pref .* (Gâ‚â€², (@SVector [Rs[1][2], -Rs[1][1]]))
    elseif D == 1
        vecs = (SVector{1,Float64}(2Ï€/first(Rs[1])),)
    else
        # The general definition of the reciprocal basis is [Gâ‚ ... Gâ‚™]áµ€ = 2Ï€[Râ‚ ... Râ‚™]â»Â¹; 
        # that form should generally be a bit slower than the above specific variants, cf. 
        # the inversion operation, so we only use it as a high-dimensional fallback. Since 
        # we use SVectors, however, either approach will probably have the same performance.
        Rm = basis2matrix(Rs)
        Gm = 2Ï€.*inv(transpose(Rm))
        vecs = ntuple(i->Gm[:,i], Val(D))
    end

    return ReciprocalBasis{D}(vecs)
end


""" 
    primitivize(Vs::Basis, sgnum::Integer) --> Rsâ€²::Basis

Transforms a conventional `Basis` (either `DirectBasis` or `ReciprocalBasis`) `Vs`
into its primitive equivalent `Vsâ€²`, provided that its centering differs from
the conventional (P or p), by inferring the Bravais type from the space group number
`sgnum` and applying an applying an appropriate (`Basis`-type specific) transformation. 
"""
function primitivize(Vs::Basis{D}, sgnum::Integer) where D
    cntr = centering(sgnum, D)
    return primitivize(Vs, cntr)
end

function transform(Rs::DirectBasis{D}, P::AbstractMatrix{<:Real}) where D
    # Rmâ€² = Rm*P (w/ Rm a matrix w/ columns of untransformed direct basis vecs Ráµ¢)
    Rmâ€² = basis2matrix(Rs)*P
    return DirectBasis{D}(ntuple(i->Rmâ€²[:,i], Val(D)))
end

"""
    primitivize(Rs::DirectBasis, cntr::Char) --> Rsâ€²::DirectBasis

Transforms a conventional DirectBasis `Rs` into its primitive 
equivalent `Rsâ€²`, with the transformation dependent on the centering
type `cntr` (P, I, F, R, A, C, and p, c); for centering P and p, the 
conventional and primive bases coincide.
"""
function primitivize(Rs::DirectBasis{D}, cntr::Char) where D
    if cntr == 'P' || cntr == 'p' # the conventional and primitive bases coincide
        return Rs
    else         
        P = primitivebasismatrix(cntr, Val(D))
        # Rmâ€² = Rm*P (w/ Rm a matrix w/ columns of conventional direct basis vecs Ráµ¢)
        return transform(Rs, P)
    end  
end

"""
    conventionalize(Rsâ€²::DirectBasis, cntr::Char) --> Rs::DirectBasis

Transforms a primitive DirectBasis `Rsâ€²` into its conventional equivalent `Rs`, with the 
transformation dependent on the centering type `cntr` (P, I, F, R, A, C, and p, c); for
centering P and p, the conventional and primive bases coincide.
"""
function conventionalize(Rsâ€²::DirectBasis{D}, cntr::Char) where D
    if cntr == 'P' || cntr == 'p' # the conventional and primitive bases coincide
        return Rsâ€²
    else         
        P = primitivebasismatrix(cntr, Val(D))
        # Rm = Rmâ€²*Pâ»Â¹ (w/ Rmâ€² a matrix w/ columns of primitive direct basis vecs Ráµ¢â€²)
        return transform(Rsâ€², inv(P)) 
    end  
end

function transform(Gs::ReciprocalBasis{D}, P::AbstractMatrix{<:Real}) where D
    # While the direct basis (ğš ğ› ğœ) transforms like 
    #       (ğšâ€² ğ›â€² ğœâ€²) = (ğš ğ› ğœ)ğ
    # under a basis change matrix ğ, the reciprocal basis (ğš* ğ›* ğœ*) transforms like 
    #       (ğš*â€² ğ›*â€² ğœ*â€²) = (ğš* ğ›* ğœ*)(ğâ»Â¹)áµ€
    # since (ğš ğ› ğœ)(ğš* ğ›* ğœ*)áµ€ = 2Ï€ğˆ must be conserved after the basis change

    # Gmâ€² = Gm*(Pâ»Â¹)áµ€ = Gm*(Páµ€)â»Â¹ (w/ Gm a matrix w/ columns of untransformed reciprocal
    # vecs Gáµ¢)
    Gmâ€² = basis2matrix(Gs)/P'
    return ReciprocalBasis{D}(ntuple(i->Gmâ€²[:,i], Val(D)))
end

"""
    primitivize(Gs::ReciprocalBasis, cntr::Char) --> Gsâ€²::ReciprocalBasis
    
Calculates the **primitive** reciprocal basis associated with an assumed **conventional**
reciprocal basis `Gs::ReciprocalBasis` with centering type `cntr`.
"""
function primitivize(Gs::ReciprocalBasis{D}, cntr::Char) where D
    if cntr == 'P' || cntr == 'p' # the conventional and primitive bases coincide
        return Gs
    else         
        P = primitivebasismatrix(cntr, Val(D))        
        return transform(Gs, P)
    end
end
function conventionalize(Gsâ€²::ReciprocalBasis{D}, cntr::Char) where D
    if cntr == 'P' || cntr == 'p' # the conventional and primitive bases coincide
        return Gs
    else         
        P = primitivebasismatrix(cntr, Val(D))        
        return transform(Gsâ€², inv(P))
    end
end

# Note that the _coefficients_ of a general ğ¤-vector transform
# differently than the reciprocal _basis_, which transforms
# from non-primed to primed variants via a basis matrix ğ
# according to (see also `primitivize(Gs::ReciprocalBasis)`):
# Specifically, a ğ¤-vector is specified by a product of a
# reciprocal basis (ğš* ğ›* ğœ*) and a coefficient vector
# (kâ‚ kâ‚‚ kâ‚ƒ)áµ€, ie. ğ¤ â‰¡ (ğš* ğ›* ğœ*)(kâ‚ kâ‚‚ kâ‚ƒ)áµ€ [note that 
# (kâ‚ kâ‚‚ kâ‚ƒ)áµ€ is a column vector].
# As a result, (kâ‚ kâ‚‚ kâ‚ƒ)áµ€ transforms like 
#     (kâ‚â€² kâ‚‚â€² kâ‚ƒâ€²)áµ€ = Páµ€ (kâ‚ kâ‚‚ kâ‚ƒ)áµ€
# since
#     ğ¤ = (ğš*â€² ğ›*â€² ğœ*â€²)(kâ‚â€² kâ‚‚â€² kâ‚ƒâ€²)áµ€     (1)  [... by definition]
#       = (ğš* ğ›* ğœ*)(ğâ»Â¹)áµ€(kâ‚â€² kâ‚‚â€² kâ‚ƒâ€²)áµ€       [... transformation of (ğš* ğ›* ğœ*) under ğ]
#       = (ğš* ğ›* ğœ*)(kâ‚ kâ‚‚ kâ‚ƒ)áµ€           (2)  [... by definition]
# then, combining (1) and (2)
#     (ğâ»Â¹)áµ€(kâ‚â€² kâ‚‚â€² kâ‚ƒâ€²)áµ€ = (kâ‚ kâ‚‚ kâ‚ƒ)áµ€
#  â‡” (kâ‚â€² kâ‚‚â€² kâ‚ƒâ€²)áµ€ = ğáµ€(kâ‚ kâ‚‚ kâ‚ƒ)áµ€

@doc raw"""
    transform(v::T, P::AbstractMatrix) where T<:AbstractVec --> vâ€²::T

Return a transformed coordinate vector `vâ€²` from an original coordinate vector `v` using a
basis change matrix `P`.

Note that a basis change matrix `P` transforms direct coordinate vectors ([`RVec`](@ref)) as
``\mathbf{r}' = \mathbf{P}^{-1}\mathbf{r}`` but transforms reciprocal coordinates
([`KVec`](@ref)) as ``\mathbf{k}' = \mathbf{P}^{\mathrm{T}}\mathbf{k}`` (see e.g. ITA7 
Secs. 1.5.1.2 and 1.5.2.1).
"""
function transform(kv::KVec{D}, P::AbstractMatrix{<:Real}) where D
    kâ‚€, kabc = parts(kv)
    return KVec{D}(P'*kâ‚€, P'*kabc)
end

function transform(rv::RVec{D}, P::AbstractMatrix{<:Real}) where D
    rcnst, rfree = parts(rv)
    return RVec{D}(P\rcnst, P\rfree)
end

@doc raw"""
    primitivize(v::T, cntr::Char) where T<:AbstractVec{D} --> vâ€²::T

Transforms a conventional coordinate vector `v` to a standard primitive basis (specified by
the centering type `cntr`), returning the primitive coordinate vector `vâ€²`.

Note that a basis change matrix ``P`` (as returned e.g. by
[`Crystalline.primitivebasismatrix`](@ref)) transforms direct coordinate vectors
([`RVec`](@ref)) as ``\mathbf{r}' = \mathbf{P}^{-1}\mathbf{r}`` but transforms reciprocal
coordinates ([`KVec`](@ref)) as ``\mathbf{k}' = \mathbf{P}^{\mathrm{T}}\mathbf{k}`` (see
e.g. ITA7 Sec. 1.5.1.2 and 1.5.2.1).
Recall also the distinction between transforming a basis and the coordinates of a vector.
"""
function primitivize(v::AbstractVec{D}, cntr::Char) where D
    if cntr == 'P' || cntr == 'p'
        return v
    else
        P = primitivebasismatrix(cntr, Val(D))
        return transform(v, P)
    end
end

"""
    conventionalize(vâ€²::T, cntr::Char) where T<:AbstractVec{D} --> v::T

Transforms a primitive coordinate vector `vâ€²` back to a standard conventional basis
(specified by the centering type `cntr`), returning the conventional coordinate vector `v`.

See also [`primitivize(::AbstractVec, ::Char`](@ref) and `transform`.
"""
function conventionalize(vâ€²::AbstractVec{D}, cntr::Char) where D
    if cntr == 'P' || cntr == 'p'
        return vâ€²
    else
        P = primitivebasismatrix(cntr, Val(D))
        return transform(vâ€², inv(P))
    end
end